
%{
    #include <iostream>
    #include <cstdlib>
    #include <string>
    #include <vector>
    #include <map>
    #include "polinomi.hpp"

    using namespace std;

    extern int yylex();

    void yyerror(string s){
        cerr << s << "!\n";
        exit(EXIT_FAILURE);
    }

    map<string, Polinom*> tablica;
%}

%union{
    double x;
    string *id;
    vector<double>* v;
    Polinom *p;
}

%token DODELA EQ NEQ
%token<id> ID 
%token<x> BROJ
%type<v> niz_brojeva
%type<p> polinom

%left EQ NEQ
%left '+' '-'
%left '*' '/'
%left UMINUS 
%left '\''

%start program

%%

program: naredba '\n' program  { } 
| naredba                      { }
;

naredba: polinom              { 
    $1->ispisi();
}
| ID DODELA polinom           { 
    tablica[*$1]=$3;
}
| polinom '[' BROJ ']'        { 
    //Koristicemo ovakav ispis kako bismo uporedili rad obe funkcije!
    cout << (*$1)[$3] << " <-> " << $1->vrednost($3) << endl;;
}
| polinom EQ polinom          { 
    if($1->jednako($3))
        cout << "True!\n";
    else
        cout << "False!\n";    
}
| polinom NEQ polinom         { 
    if($1->razlicito($3))
        cout << "True!\n";
    else
        cout << "False!\n";
}
|                             { }
;

polinom: polinom '+' polinom  { 
    $$=$1->saberi($3);
}
| polinom '-' polinom         { 
    $$=$1->oduzmi($3);
}
| polinom '*' polinom         { 
    $$=$1->pomnozi($3);
}
| '(' polinom ')'             { 
    $$=$2;
}
| '-' %prec UMINUS polinom    { 
    $$=$2->negiraj();
}
| polinom '\''                { 
    $$=$1->izvod();
}
| '$' polinom '|' BROJ        { 
    $$=$2->integral($4);
}
| '<' niz_brojeva '>'         { 
    $$=new Polinom(*$2);
}
| ID                          { 
    if(tablica.find(*$1)==tablica.end()){
        cerr << "Polinom jos uvek nije definisan!\n";
        exit(EXIT_FAILURE);
    }
    $$=tablica[*$1];
} 
;

niz_brojeva: niz_brojeva ',' BROJ { 
    $$=$1;
    $$->push_back($3);
}
| BROJ                            { 
    $$=new vector<double>();
    $$->push_back($1);
}
;

%%

int main(){

    if(yyparse()==0){
        cout << "Sve je u redu sa unetim naredbama!\n";
    } else {
        cerr << "Sintaksna greska!\n";
        exit(EXIT_FAILURE);
    }

    return 0;
}